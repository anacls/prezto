#
# A personalized theme created mixing skwp and sorin themes. 
# More git info than skwp and no user, hostname and ruby version info.
#

function prompt_nico_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_nico_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_nico_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_nico_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_nico_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_nico_colors[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${_prompt_nico_colors[5]}✚%f"
  zstyle ':prezto:module:git:info:ahead' format "${_prompt_nico_colors[5]}⬆%f" 
  zstyle ':prezto:module:git:info:behind' format "${_prompt_nico_colors[2]}⬇%f"
  zstyle ':prezto:module:git:info:deleted' format "${_prompt_nico_colors[2]}✖%f"
  zstyle ':prezto:module:git:info:modified' format "${_prompt_nico_colors[4]}✱%f"
  zstyle ':prezto:module:git:info:untracked' format "${_prompt_nico_colors[3]}●%f"
  zstyle ':prezto:module:git:info:renamed' format "${_prompt_nico_colors[3]}➜%f"
  zstyle ':prezto:module:git:info:stashed' format "${_prompt_nico_colors[1]}✭f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # Define prompts.
  PROMPT="${_prompt_nico_colors[5]}%~%f "'${git_info:+${(e)git_info[prompt]}}'"$ "
}

prompt_nico_setup "$@"
# vim: ft=zsh
